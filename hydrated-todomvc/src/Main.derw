import "../derw-packages/derw-lang/html/src/Html" as Html exposing ( HtmlNode, RunningProgram, div, text, program, attribute, class_ )
import "../derw-packages/derw-lang/stdlib/src/List" as List
import "../derw-packages/derw-lang/stdlib/src/Maybe" as Maybe exposing ( Maybe, Just, Nothing )
import "../derw-packages/derw-lang/stdlib/src/Number" as Number
import "../derw-packages/derw-lang/stdlib/src/String" as String
import "../derw-packages/derw-lang/stdlib/src/Task" as Task
import "../derw-packages/derw-lang/web/src/LocalStorage" as LocalStorage

exposing (initialModel, Model, view, ListItem)

type alias ListItem = {
    id: number,
    label: string,
    completed: boolean
}

type FilterMode =
    All
    | Active
    | Completed

type alias Model = {
    list: List ListItem,
    input: string,
    filterMode: FilterMode,
    editing: Maybe ListItem
}

initialModel: Model
initialModel =
    {
        list: [ ],
        input: "",
        filterMode: All,
        editing: Nothing
    }

type Msg =
    Noop
    | ToggleAll { state: boolean }
    | ToggleCompleted { item: ListItem }
    | DestroyItem { item: ListItem }
    | AddItem { label: string }
    | SetNewItemInput { input: string }
    | SetFilterMode { mode: FilterMode }
    | ClearCompleted
    | Edit { item: ListItem }
    | CancelEdit
    | Save { item: ListItem }
    | Focus

toggleListItem: ListItem -> ListItem -> ListItem
toggleListItem itemToToggle maybeItem =
    if itemToToggle.id == maybeItem.id then
        { ...maybeItem, completed: !maybeItem.completed }
    else
        maybeItem

storeListItems: List ListItem -> void
storeListItems items =
    do
        encodedItems: string
        encodedItems =
            List.map (\item -> `${item.id}-${item.completed}-${item.label}`) items
                |> String.join "&"

        LocalStorage.setItem "todomvc-derw" encodedItems
    return
        undefined

decodeItem: string -> Maybe ListItem
decodeItem str =
    case String.split "-" str of
        id :: rest ->
            case rest of
                completed :: ys ->
                    case Number.parseInt id 10 of
                        Nothing ->
                            Nothing

                        Just { value } ->
                            Just { value: {
                                id: value,
                                completed: completed == "true",
                                label: String.join "-" ys
                            } }

                default ->
                    Nothing

        default ->
            Nothing

loadListItems: any -> List ListItem
loadListItems _ =
    do
        stored: string
        stored =
            LocalStorage.getItem "todomvc-derw"
                |> Maybe.withDefault ""

        decodedItems: List ListItem
        decodedItems =
            String.split "&" stored
                |> List.filterMap decodeItem
    return
        decodedItems

max: number -> List number -> number
max start xs =
    case xs of
        x :: ys ->
            if start < x then
                max x ys
            else
                max start ys

        default ->
            start

filteredItems: FilterMode -> List ListItem -> List ListItem
filteredItems filterMode items =
    case filterMode of
        All ->
            items

        Active ->
            List.filter (\item -> !item.completed) items

        Completed ->
            List.filter (\item -> item.completed) items

update: Msg -> Model -> (Msg -> void) -> Model
update msg model send =
    case msg of
        Noop ->
            model

        ToggleAll { state } ->
            let
                updatedItems: List ListItem
                updatedItems =
                    List.map (\x -> { ...x, completed: state }) model.list

                store: void
                store =
                    storeListItems updatedItems
            in
                { ...model, list: updatedItems }

        ToggleCompleted { item } ->
            let
                updatedItems: List ListItem
                updatedItems =
                    List.map (\x -> toggleListItem item x) model.list

                store: void
                store =
                    storeListItems updatedItems
            in
                { ...model, list: updatedItems }

        DestroyItem { item } ->
            let
                withoutItem: List ListItem
                withoutItem =
                    List.filter (\x -> x.id != item.id) model.list

                store: void
                store =
                    storeListItems withoutItem
            in
                { ...model, list: withoutItem }

        AddItem { label } ->
            if label.trim() == "" then
                model
            else
                let
                    newId: number
                    newId =
                        List.map (\item -> item.id) model.list
                            |> max 0
                            |> (\x -> x + 1)

                    newItem: ListItem
                    newItem =
                        {
                            id: newId,
                            label: label,
                            completed: false
                        }

                    updatedItems: List ListItem
                    updatedItems =
                        newItem :: model.list

                    store: void
                    store =
                        storeListItems updatedItems
                in
                    {
                        ...model,
                        list: updatedItems,
                        input: ""
                    }

        SetNewItemInput { input } ->
            { ...model, input: input }

        SetFilterMode { mode } ->
            { ...model, filterMode: mode }

        ClearCompleted ->
            let
                updatedItems: List ListItem
                updatedItems =
                    filteredItems Active model.list

                store: void
                store =
                    storeListItems updatedItems
            in
                { ...model, list: updatedItems }

        Edit { item } ->
            let
                focus: void
                focus =
                    Task.after (\_ -> send Focus) 30
                        |> Task.runTask
            in
                { ...model, editing: Just { value: item } }

        CancelEdit ->
            { ...model, editing: Nothing }

        Focus ->
            let
                element: any
                element =
                    document.getElementById "editor"

                focus: any
                focus =
                    element.focus()
            in
                model

        Save { item } ->
            let
                updater: ListItem -> ListItem
                updater currentItem =
                    if currentItem.id == item.id then
                        item
                    else
                        currentItem

                updatedItems: List ListItem
                updatedItems =
                    List.map updater model.list

                maybeWithoutItem: List ListItem
                maybeWithoutItem =
                    if item.label.trim() == "" then
                        List.filter (\x -> x.id != item.id) updatedItems
                    else
                        updatedItems

                store: void
                store =
                    storeListItems maybeWithoutItem
            in
                {
                    ...model,
                    editing: Nothing,
                    list: maybeWithoutItem
                }

viewHeader: Model -> HtmlNode Msg
viewHeader model =
    let
        onKeydown: any -> Msg
        onKeydown event =
            if event.key == "Enter" then
                AddItem { label: event.target.value }
            else
                SetNewItemInput { input: event.target.value }
    in
        Html.header [ ] [ class_ "header" ] [
            Html.h1 [ ] [ ] [ text "todos" ],
            Html.input [ Html.onWithOptions "keydown" onKeydown false false ] [
                class_ "new-todo",
                Html.attribute "placeholder" "What needs to be done?",
                Html.attribute "autofocus" "true",
                Html.attribute "value" model.input
            ]
        ]

viewListItem: Model -> ListItem -> HtmlNode Msg
viewListItem model item =
    let
        className: string
        className =
            case model.editing of
                Just { value } ->
                    if value.id == item.id then
                        "editing"
                    else
                        if item.completed then
                            "completed"
                        else
                            ""

                Nothing ->
                    if item.completed then
                        "completed"
                    else
                        ""

        editingText: string
        editingText =
            case model.editing of
                Just { value } ->
                    value.label

                Nothing ->
                    ""

        onEdit: any -> Msg
        onEdit event =
            Edit { item: { ...item, label: event.target.value } }

        onSave: any -> Msg
        onSave event =
            case model.editing of
                Nothing ->
                    Save { item: item }

                Just { value } ->
                    Save { item: { ...value, label: event.target.value } }

        onKeydown: any -> Msg
        onKeydown event =
            if event.key == "Enter" then
                onSave event
            else
                Edit { item: { ...item, label: event.target.value } }

        viewItemLabel: HtmlNode Msg
        viewItemLabel =
            Html.label [ Html.onWithOptions "dblclick" (\_ -> Edit { item: item }) false false ] [ ] [ text item.label ]
    in
        Html.li [ ] [ class_ className ] [
            div [ ] [ class_ "view" ] [
                Html.input [ Html.onWithOptions "click" (\_ -> ToggleCompleted { item: item }) false false ] [
                    class_ "toggle",
                    Html.attribute "type" "checkbox",
                    Html.booleanAttribute "checked" item.completed
                ],
                viewItemLabel,
                Html.button [ Html.onClick (\_ -> DestroyItem { item: item }) ] [ class_ "destroy" ] [ ]
            ],
            Html.input [
                Html.onWithOptions "keydown" onKeydown false false,
                Html.on "blur" onSave
            ] [
                class_ "edit",
                Html.attribute "id" "editor",
                Html.attribute "value" editingText
            ]
        ]

viewList: Model -> HtmlNode Msg
viewList model =
    let
        items: List ListItem
        items =
            filteredItems model.filterMode model.list
    in
        Html.ul [ ] [ class_ "todo-list" ] (List.map (\item -> viewListItem model item) items)

viewMain: Model -> HtmlNode Msg
viewMain model =
    Html.section [ ] [ class_ "main" ] [
        Html.input [ Html.onWithOptions "click" (\event -> ToggleAll { state: event.target.checked }) false false ] [
            Html.attribute "id" "toggle-all",
            class_ "toggle-all",
            Html.attribute "type" "checkbox"
        ],
        Html.label [ ] [ Html.attribute "for" "toggle-all" ] [ text "Mark all as complete" ],
        viewList model
    ]

viewFooter: Model -> HtmlNode Msg
viewFooter model =
    let
        activeItems: List ListItem
        activeItems =
            filteredItems Active model.list

        completedItems: List ListItem
        completedItems =
            filteredItems Completed model.list

        maybeClearButton: HtmlNode Msg
        maybeClearButton =
            if completedItems.length == 0 then
                text ""
            else
                Html.button [ Html.onClick (\_ -> ClearCompleted) ] [ class_ "clear-completed" ] [ text "Clear completed" ]

        setModeEventHandler: FilterMode -> Html.Event Msg
        setModeEventHandler mode =
            Html.onWithOptions "click" (\_ -> SetFilterMode { mode: mode }) false false

        itemsLeftText: string
        itemsLeftText =
            if activeItems.length == 1 then
                `${activeItems.length} item left`
            else
                `${activeItems.length} items left`

        allClass: string
        allClass =
            case model.filterMode of
                All ->
                    "selected"

                default ->
                    ""

        activeClass: string
        activeClass =
            case model.filterMode of
                Active ->
                    "selected"

                default ->
                    ""

        completedClass: string
        completedClass =
            case model.filterMode of
                Completed ->
                    "selected"

                default ->
                    ""
    in
        Html.footer [ ] [ class_ "footer" ] [
            Html.span [ ] [ class_ "todo-count" ] [ Html.strong [ ] [ ] [ text itemsLeftText ] ],
            Html.ul [ ] [ class_ "filters" ] [
                Html.li [ ] [ ] [ Html.a [ setModeEventHandler All ] [
                    class_ allClass,
                    Html.attribute "href" "#/"
                ] [ text "All" ] ],
                Html.li [ ] [ ] [ Html.a [ setModeEventHandler Active ] [
                    class_ activeClass,
                    Html.attribute "href" "#/active"
                ] [ text "Active" ] ],
                Html.li [ ] [ ] [ Html.a [ setModeEventHandler Completed ] [
                    class_ completedClass,
                    Html.attribute "href" "#/completed"
                ] [ text "Completed" ] ]
            ],
            maybeClearButton
        ]

view: Model -> HtmlNode Msg
view model =
    if model.list.length == 0 then
        Html.section [ ] [
            class_ "todoapp",
            Html.attribute "id" "section"
        ] [ viewHeader model ]
    else
        Html.section [ ] [
            class_ "todoapp",
            Html.attribute "id" "section"
        ] [
            viewHeader model,
            viewMain model,
            viewFooter model
        ]

main: void -> RunningProgram Model Msg
main _0 =
    let
        filterMode: FilterMode
        filterMode =
            urlParser window.location.hash

        dataRoot: any
        dataRoot =
            document.getElementById "data"

        dataModel: Model
        dataModel =
            globalThis.JSON.parse dataRoot.innerHTML

        p: RunningProgram Model Msg
        p =
            program {
                initialModel: {
                ...dataModel,
    
            },
                view: view,
                update: update,
                root: "hydration"
            }

        root: any
        root =
            document.getElementById "root"

        canceller: any -> void
        canceller event =
            if event.key == "Escape" then
                p.send CancelEdit
            else
                undefined

        attach: void
        attach =
            document.addEventListener "keydown" canceller

        hydrate: void
        hydrate =
            Html.hydrate p root

        urlPage: void
        urlPage =
            p.send (SetFilterMode { mode: filterMode })
    in
        p

runMain: any
runMain =
    if globalThis.document == undefined then
        undefined
    else
        main()

urlParser: string -> FilterMode
urlParser path =
    case path of
        "#/completed" ->
            Completed

        "#/active" ->
            Active

        default ->
            All
